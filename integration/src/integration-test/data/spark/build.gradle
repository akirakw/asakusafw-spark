group 'com.example'

buildscript {
    repositories {
        if (System.getProperty("maven.local", "true") == "true") {
            mavenLocal()
        }
        maven { url 'https://asakusafw.s3.amazonaws.com/maven/releases' }
        maven { url 'https://asakusafw.s3.amazonaws.com/maven/snapshots' }
    }
    dependencies {
        classpath group: 'com.asakusafw.spark', 
                  name: 'asakusa-spark-gradle',
                  version: System.getProperty("asakusafw-spark.version")
    }
}

if (System.getProperty("maven.local", "true") == "true") {
    repositories {
        mavenLocal()
    }
}

apply plugin: 'asakusafw-sdk'
apply plugin: 'asakusafw-organizer'
apply plugin: 'asakusafw-spark'
apply plugin: 'eclipse'

asakusafw {
    sdk.incubating = System.getProperty('sdk.incubating', 'false') != 'false'
    sdk.testkit 'spark'
}

asakusafwOrganizer {
    hadoop {
        embed !System.getenv('HADOOP_CMD')
    }
    profiles.dev {
        assembly.into('.') {
            from 'src/main/conf'
            replace([
                'project.home.path' : file(System.getenv('PROJECT_HOME')).absolutePath,
                'project.home.uri' : uri(System.getenv('PROJECT_HOME')).toString(),
            ])
        }
    }
}

test {
    testLogging {
        showStandardStreams true
        showExceptions true
        showStackTraces true
        showCauses true
        exceptionFormat 'full'
    }
}

if (System.getProperty('hive.version')) {
    asakusafw {
        sdk.hive true
    }
    asakusafwOrganizer {
        hive.enabled true
    }
    if (System.getProperty('hive.version', 'default') != 'default') {
        asakusafwOrganizer {
            hive.libraries = ["org.apache.hive:hive-exec:${System.getProperty('hive.version')}"]
        }
    }
}
